
/**
 * Demonstrates TxAdvanced, with external funding (additional input) and a change output
 */
contract AdvancedCounter {
    @state
    int counter;

    public function increment(SigHashPreimage txPreimage) {
        // update counter state
        this.counter++;
        
        // ensure output matches what we expect:
        // - amount is same as specified
        // - output script is the same as scriptCode except the counter was incremented
        int amount = SigHash.value(txPreimage);
        require(this.propagateState(txPreimage, amount));
    }

    function propagateState(SigHashPreimage txPreimage, int amount) : bool {
        SigHashType sigHashType = SigHash.ANYONECANPAY | SigHash.SINGLE | SigHash.FORKID;
        // this ensures the preimage is for the current tx
        require(Tx.checkPreimageSigHashType(txPreimage, sigHashType));
        bytes outputScript = this.getStateScript();
        bytes output = Utils.buildOutput(outputScript, amount);
        return (hash256(output) == SigHash.hashOutputs(txPreimage));
    }
}
